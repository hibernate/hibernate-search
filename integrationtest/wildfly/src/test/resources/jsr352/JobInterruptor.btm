RULE Enable the job interruptor as needed
CLASS org.hibernate.search.test.integration.jsr352.massindexing.test.util.JobInterruptorUtil
METHOD enable
AT ENTRY
IF true
DO flag("job-interruptor-enabled");
return
ENDRULE

RULE Disable the job interruptor as needed
CLASS org.hibernate.search.test.integration.jsr352.massindexing.test.util.JobInterruptorUtil
METHOD disable
AT ENTRY
IF true
DO clear("job-interruptor-enabled");
return
ENDRULE

RULE Create count-down before the step partitioning
CLASS org.hibernate.search.jsr352.massindexing.impl.steps.lucene.PartitionMapper
METHOD mapPartitions
AT EXIT
IF flagged("job-interruptor-enabled")
DO createCountDown("beforeRestart", 2500)
ENDRULE

RULE Count down for each item read, interrupt the job when counter is 0
CLASS org.hibernate.search.jsr352.massindexing.impl.steps.lucene.EntityReader
METHOD readItem
AT ENTRY
IF flagged("job-interruptor-enabled")
AND countDown("beforeRestart")
DO throw new java.lang.InterruptedException("Job is interrupted by Byteman.")
ENDRULE
